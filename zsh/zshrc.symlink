# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Repos
export EDITOR=vim

export PATH=$HOME/.cabal/bin:$PATH

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U $DOTFILES/functions/*(:t)
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# History
HISTFILE=~/.zsh_history
HISTSIZE=10000
SAVEHIST=10000

# oh-my-zsh
ZSH=$HOME/.oh-my-zsh
ZSH_THEME="af-magic"

plugins=(git git-flow npm ssh-agent colorize colored-man composer extract zsh-syntax-highlighting)

source $ZSH/oh-my-zsh.sh
